buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.1.1"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["BidmadRnPlugin_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["BidmadRnPlugin_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.bidmadrnplugin"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

rootProject.allprojects {
  repositories {
    google()
    mavenCentral()
    maven { url 'https://devrepo.kakao.com/nexus/content/groups/public/' } //Adift
    maven { url "https://bidmad-sdk.s3.amazonaws.com/" } //Bidmad
    maven { url "https://dl-maven-android.mintegral.com/repository/mbridge_android_sdk_oversea" } //Mintegral
    maven { url 'https://artifact.bytedance.com/repository/pangle/' } //Pangle
    maven { url "https://teads.jfrog.io/artifactory/SDKAndroid-maven-prod" } //Teads
    maven { url 'https://taboolapublic.jfrog.io/artifactory/mobile-release'} //Taboola
  }
}

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-android:+"

  implementation 'ad.helper.openbidding:admob-obh:3.23.0'
  implementation 'com.adop.sdk:bidmad-androidx:3.23.0'
  implementation 'com.adop.sdk.adapter:adfit:3.19.5.0'
  implementation 'com.adop.sdk.adapter:admixer:1.0.9.0'
  implementation 'com.adop.sdk.adapter:admob:24.4.0.0'
  implementation 'com.adop.sdk.adapter:adpopcorn:3.8.2.0'
  implementation 'com.adop.sdk.adapter:applovin:13.3.1.0'
  implementation 'com.adop.sdk.adapter:coupang:1.0.0.5'
  implementation 'com.adop.sdk.adapter:fyber:8.3.7.0'
  implementation 'com.adop.sdk.adapter:ortb:1.0.1'
  implementation 'com.adop.sdk.adapter:mobwith:1.1.3'
  implementation 'com.adop.sdk.adapter:pangle:7.2.0.6.0'
  implementation 'com.adop.sdk.adapter:taboola:4.0.8.0'
  implementation 'com.adop.sdk.adapter:unityads:4.15.0.0'
  implementation 'com.adop.sdk.adapter:vungle:7.5.0.0'
  implementation 'com.adop.sdk.partners:admobbidding:1.1.0'
}
