buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["BidmadRnPlugin_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["BidmadRnPlugin_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.bidmadrnplugin"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

rootProject.allprojects {
  repositories {
    google()
    mavenCentral()
    maven { url 'https://devrepo.kakao.com/nexus/content/groups/public/' } //Adift
    maven { url "https://bidmad-sdk.s3.amazonaws.com/" } //Bidmad
    maven { url 'https://android-sdk.is.com/' } // IronSource
    maven { url "https://dl-maven-android.mintegral.com/repository/mbridge_android_sdk_oversea" } //Mintegral
    maven { url 'https://artifact.bytedance.com/repository/pangle/' } //Pangle
    maven { url 'https://repo.pubmatic.com/artifactory/public-repos' } //PubMatic
    maven { url "https://sdk.tapjoy.com/" } //Tapjoy
  }
}

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"

  implementation 'ad.helper.openbidding:admob-obh:3.15.0'
  implementation 'com.adop.sdk:bidmad-androidx:3.15.0'
  implementation 'com.adop.sdk.adapter:adfit:3.12.15.2'
  implementation 'com.adop.sdk.adapter:admob:22.0.0.5'
  implementation 'com.adop.sdk.adapter:applovin:11.9.0.3'
  implementation 'com.adop.sdk.adapter:coupang:1.0.0.2'
  implementation 'com.adop.sdk.adapter:criteo:6.0.0.1'
  implementation 'com.adop.sdk.adapter:fyber:8.2.3.3'
  implementation 'com.adop.sdk.adapter:ironsource:7.3.0.0'
  implementation 'com.adop.sdk.adapter:pangle:5.2.1.1.2'
  implementation 'com.adop.sdk.adapter:pubmatic:2.7.1.3'
  implementation 'com.adop.sdk.adapter:unityads:4.6.1.4'
  implementation 'com.adop.sdk.adapter:vungle:6.12.1.2'
  implementation 'com.adop.sdk.partners:admobbidding:1.0.2'
}

